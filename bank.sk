options:
    name: &6&lSLOGIC &7|

on citizen right click:
    if npc's name is "&6Banker":
        set {bankbal::%uuid of player%} to 0 if {bankbal::%uuid of player%} is not set
        open virtual chest inventory with 4 rows named "&6&lBank" to player
        format gui slot (all integers between 0 and 35) of player with gray stained glass pane
        format gui slot 11 of player with barrel named "&6Deposit Points" with lore "&7Current balance: &6%{bankbal::%uuid of player%}%" to close then run function toggleDep(player, true)
        format gui slot 13 of player with dropper named "&6Withdraw Points" with lore "&7Current balance: &6%{bankbal::%uuid of player%}%" to close then run function toggleWith(player, true)
        format gui slot 15 of player with gold nugget named "&6Bank Transfer" with lore "&7Send points to other bank accounts (uses your bank balance)" and "&7Current balance: &6%{bankbal::%uuid of player%}%" to close then run function togglePay(player)
        format gui slot 31 of player with barrier named "&cClose" to close

function toggleDep(p: player, b: boolean):
    set {_up} to uuid of {_p}
    set {bankdep.%{_up}%} to {_b}
    send "{@name} How much would you like to deposit? (Type all to deposit all, type cancel to cancel)" to {_p}

function toggleWith(p: player, b: boolean):
    set {_up} to uuid of {_p}
    set {bankwith.%{_up}%} to {_b}
    send "{@name} How much would you like to withdraw? (Type all to withdraw all, type cancel to cancel)" to {_p}
    

function togglePay(p: player):
    send "{@name} Use /pay <username> <amount>" to {_p}

on chat:
    if {bankdep.%uuid of player%} is true:
        cancel event
        if message is "cancel":
            send "{@name} &7Canceling"
            delete {bankdep.%uuid of player%} 
            exit
        if message is "all":
            if {points::%uuid of player%} < 0:
                play sound "ENTITY_VILLAGER_NO" to player
                send "{@name} &cYour points are less than 0, contact a manager for your points to be reset!"
                delete {bankdep.%uuid of player%}
            else:
                send "{@name} &7Deposting &6all (%{points::%uuid of player%}%&6)"
                add {points::%uuid of player%} to {bankbal::%uuid of player%}
                set {points::%uuid of player%} to 0
                refreshScoreboard(player)
                play sound "ENTITY_ARROW_HIT_PLAYER" to player
                delete {bankdep.%uuid of player%}
        else:
            set {_in} to message parsed as number
            if {_in} is not set:
                parse error is set:
                    send "{@name} &cLine 1 is invalid: %last parse error%"
                else:
                    send "{@name} &cPlease provide a valid amount"
            else:
                set {_in} to abs({_in})
                if {_in} > {points::%uuid of player%}:
                    play sound "ENTITY_VILLAGER_NO" to player
                    send "{@name} &cThat amount is greater than your current balance"
                    delete {bankdep.%uuid of player%}
                    exit
                else:
                    set {bankbal::%uuid of player%} to {bankbal::%uuid of player%} + {_in}
                    set {points::%uuid of player%} to {points::%uuid of player%} - {_in}
                    refreshScoreboard(player)
                    play sound "ENTITY_ARROW_HIT_PLAYER" to player
            delete {bankdep.%uuid of player%}
    
    else if {bankwith.%uuid of player%} is true:
        cancel event
        if message is "cancel":
            send "{@name} &7Canceling"
            delete {bankdep.%uuid of player%} 
            exit
        if message is "all":
            send "{@name} &7Withdrawing &6all (%{bankbal::%uuid of player%}%&6)"
            add {bankbal::%uuid of player%} to {points::%uuid of player%}
            set {bankbal::%uuid of player%} to 0
            refreshScoreboard(player)
            play sound "ENTITY_ARROW_HIT_PLAYER" to player
            delete {bankwith.%uuid of player%}
        else:
            set {_in} to message parsed as number
            if {_in} is not set:
                parse error is set:
                    send "{@name} &cLine 1 is invalid: %last parse error%"
                else:
                    send "{@name} &cPlease provide a valid amount"
            else:
                if {_in} > {bankbal::%uuid of player%}:
                    play sound "ENTITY_VILLAGER_NO" to player
                    send "{@name} &cThat amount is greater than your current bank balance"
                    delete {bankwith.%uuid of player%}
                    exit
                else:
                    set {_in} to abs({_in})
                    set {bankbal::%uuid of player%} to {bankbal::%uuid of player%} - {_in}
                    set {points::%uuid of player%} to {points::%uuid of player%} + {_in}
                    refreshScoreboard(player)
                    play sound "ENTITY_ARROW_HIT_PLAYER" to player
            delete {bankwith.%uuid of player%}

command /pay [<offline player>] [<number>]:
    aliases: transfer, send
    trigger:
        if arg-1 is not set:
            send "{@name} &cYou need to provide a player to transfer points"
        else if arg-2 is not set:
            send "{@name} &cYou need to provide an amount of points"
        else if arg-2 <= 0:
            send "{@name} &cYou cannot give 0 points or negative points"
        else if arg-2 > {bankbal::%uuid of player%}:
            send "{@name} &cYou cannot give more points than your current bank balance"
        else:
            set {bankbal::%uuid of player%} to {bankbal::%uuid of player%} - arg-2
            set {bankbal::%uuid of arg-1%} to {bankbal::%uuid of arg-1%} + arg-2
            send "{@name} &7Transfered &6%arg-2% &7points to &6%arg-1%"
            play sound "ENTITY_ARROW_HIT_PLAYER" to player
            if arg-1 is online:
                send "{@name} &6%player% &7transfered &6%arg-2% &7points to your account" to arg-1
                play sound "ENTITY_ARROW_HIT_PLAYER" to arg-1

command /bank [<text>] [<offline player>] [<number>]:
    description: Allows you to view your bank and admins to modify 
    trigger:
        if arg-1 is not set:
            send "{@name} &7Your bank balance: &6%{bankbal::%uuid of player%}%" to player
        else if player has permission "points.admin":
            if arg-2 is not set:
                send "{@name} &cYou need to specify a player" to player
            else if arg-1 is "reset":
                send "{@name} &7Resetting &6%arg-2%&7's bank" to player
                set {bankbal::%uuid of arg-2%} to 0
            else if arg-1 is "show" or "amount":
                send "{@name} &6%arg-2% &7has &6%{bankbal::%uuid of arg-2%}% &7points in their bank" to player
            else if arg-3 is not set:
                send "{@name} &cYou need to specify an amount" to player
            else:
                if arg-1 is "give" or "add":
                    send "{@name} &7Adding &6%arg-3% &7to &6%arg-2%&7's bank" to player
                    add arg-3 to {bankbal::%uuid of arg-2%}
                    send "{@name} &7You have been given &6%arg-3% &7point(s) in your bank." to arg-2
                else if arg-1 is "remove":
                    send "{@name} &7Removing &6%arg-3% &7points from &6%arg-2%&7's bank balance" to player
                    remove arg-3 from {bankbal::%uuid of arg-2%}
                else if arg-1 is "set":
                    send "{@name} &7Set &6%arg-2%&7's bank points to &6%arg-3%" to player
                    set {bankbal::%uuid of arg-2%} to arg-3

command /plimit:
    permission: points.admin
    trigger:
        loop {points::*}:
            if loop-value > 100000:
                send "%loop-index parsed as offline player%: %loop-value%"
            else if loop-value < 0:
                send "%loop-index parsed as offline player%: %loop-value%"

command /blimit:
    permission: points.admin
    trigger:
        loop {bankbal::*}:
            if loop-value > 100000:
                send "%loop-index parsed as offline player%: %loop-value%"
            else if loop-value < 0:
                send "%loop-index parsed as offline player%: %loop-value%"